plugins {
    id 'java'
}

group = 'hello'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    //스프링 MVC 추가
    implementation 'org.springframework:spring-webmvc:6.0.4'

    //내장 톰켓 라이브러리 추가 (애플리케이션 시작할때 직접 해당 라이브러리에 접근하여 톰켓을 띄움)
    // 서블릿 관련 코드들도 포함하고 있음
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

/** 일반 Jar 생성 (권장X)
 * jar안에는 jar를 포함하지 못한다. (포함해도 인식X)
 * 대안으로는 라이브러리 jar파일을 모두 구하여 MANIFEST파일에 경로를 적어주면 되지만 매우 번거롭다.
 * 라이브러리 역할의 jar 파일도 항상 가지고 다녀야함.
 */
task buildJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'hello.embed.EmbedTomcatSpringMain' // 실행할 main() 의 클래스 지정
    }
    with jar
}

//Fat Jar 생성
task buildFatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'hello.embed.EmbedTomcatSpringMain'
    }
    duplicatesStrategy = DuplicatesStrategy.WARN
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
